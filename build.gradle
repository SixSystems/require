plugins {
    id 'java'
    id 'idea'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'maven'
    id 'maven-publish'
}

group 'com.github.ilubenets'
version '1.0'
sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.code.findbugs:jsr305:3.0.+'
    // test
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.1.+'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.1.+'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.1.+'
}

///////// Bintray

final Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintrayUser')
    key = properties.getProperty('bintrayApiKey')
    publications = ['jar']
    publish = true
    pkg {
        repo = 'maven'
        name = 'require'
        userOrg = user
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/iLubenets/require'
        issueTrackerUrl = 'https://github.com/iLubenets/require/issues'
        vcsUrl = 'https://github.com/iLubenets/require.git'
        githubRepo = 'iLubenets/require'
        githubReleaseNotesFile = 'CHANGELOG.md'
        labels = ['assert']
        publicDownloadNumbers = false
        version {
            name = project.version
            desc = 'Assertion lib'
            vcsTag = 'master'
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier 'sources'
}

publishing {
    publications {
        jar(MavenPublication) {
            from components.java
            artifact sourceJar
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}

bintrayUpload.dependsOn(build)
